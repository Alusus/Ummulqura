اشمل "مـتم/طـرفية"؛
اشمل "مـتم/وقـت"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـظام"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Ummulqura"، "هـجري.أسس")؛
اشمل "بـناء"؛
استخدم مـتم؛

دالة حول_من_ميلادي_إلى_هجري (تاريخ_ميلادي: مـؤشر_محارف) {
    عرف عناصر_التاريخ: مـصفوفة[نـص] = نـص(تاريخ_ميلادي).قطع("-")؛
    إذا عناصر_التاريخ.هات_الطول() != 3 {
        نـظام.فشل(1، "تاريخ غير صالح\ج")؛
    }
    عرف تاريخ: هـجري.تـاريخ = هـجري.حول_لهجري(
        نـص.اقرأ_صحيح(عناصر_التاريخ(0)),
        نـص.اقرأ_صحيح(عناصر_التاريخ(1)),
        نـص.اقرأ_صحيح(عناصر_التاريخ(2))
    )؛
    طـرفية.اطبع("%d-%d-%d\n", تاريخ.السنة, تاريخ.الشهر, تاريخ.اليوم)؛
}

دالة اعرض_التاريخ_الحالي {
    عرف خز: صـحيح_متكيف؛
    خز = وقـت.هات_الختم_الزمني(0)؛
    عرف وقت: مؤشر[وقـت.وقـت_مفصل]؛
    وقت = وقـت.هات_وقـت_مفصل(خز~مؤشر)؛
    عرف تاريخ: هـجري.تـاريخ = هـجري.حول_لهجري(
        وقت~محتوى.سنة + 1900,
        وقت~محتوى.شهر + 1,
        وقت~محتوى.يوم
    )؛
    طـرفية.اطبع("%d-%d-%d\n", تاريخ.السنة, تاريخ.الشهر, تاريخ.اليوم)؛
}

@تصدير[main]
دالة ابدأ (عدد_المعطيات: صـحيح، المعطيات: مؤشر[مصفوفة[مـؤشر_محارف]]): صـحيح {
    إذا عدد_المعطيات >= 2 {
        حول_من_ميلادي_إلى_هجري(المعطيات~محتوى(1))؛
    } وإلا {
        اعرض_التاريخ_الحالي()؛
    }
    أرجع 0؛
}

إذا الـعملية.عدد_المعطيات == 3 {
    عرف المعطى: مؤشر[مصفوفة[مـحرف]] = الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1)؛
    إذا المعطى~محتوى(0) >= '0' و المعطى~محتوى(0) <= '9' {
        حول_من_ميلادي_إلى_هجري(المعطى)؛
    } وإلا {
        إذا بـناء.أنشئ_تنفيذي(ابدأ~شبم، المعطى) {
            مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
        }
    }
} وإلا {
    اعرض_التاريخ_الحالي()؛
}

